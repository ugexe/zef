#! perl6
use v6;

# use Zef::Utils::SystemInfo; # not used yet
use Zef::App;
use Zef::Distribution::DependencySpecification;

#| Download specific distributions
multi MAIN('fetch', Bool :$depends, Bool :$test-depends, Bool :$build-depends, Bool :v(:$verbose), *@identities) {
    my $app = Zef::App.new(:$depends, :$test-depends, :$build-depends);
    my @candidates = |$app.candidates(|@identities>>.&STR2IDENTITY);
    $app.fetch(|@candidates);
}

#| Run tests
multi MAIN('test', Bool :$force, Bool :v(:$verbose), *@paths) {
    my $app = Zef::App.new(:$verbose, :$force, :$verbose);
    my %results = $app.test(|@paths);
    %results<fail>.elems ?? exit(1) !! exit(0);
}

#| Install
multi MAIN('install', Bool :$depends = True, Bool :$test-depends = True, Bool :$build-depends = True,
                      Bool :v(:$verbose), Bool :$force, Bool :$test = True, Bool :$fetch = True,
                      Bool :$dry, Bool :$update, Bool :$upgrade, :$install-to = ['site'], *@identities) {

    my $app = Zef::App.new(:$force, :$verbose, :$depends, :$test-depends, :$build-depends);
    $app.install( :$fetch, :$install-to, :$test, :$update, :$upgrade, :$dry, |@identities>>.&STR2IDENTITY );
}

#| Find that shit
multi MAIN('search', *@identities, *%fields) {
    my $app = Zef::App.new;
    say $app.search(|@identities>>.&STR2IDENTITY, |%fields);
}

#| Update package indexes
multi MAIN(*@names) {
    my $app = Zef::App.new;
    $app.storage.update(|@names)
}

multi MAIN(Bool :$help?) {
    note q:to/END_USAGE/
        Zef - Perl6 Module Management

        USAGE

          zef [flags|options] command [package]


        COMMANDS

          install                Install specific dependencies by name or path
          test                   Run tests on a given module's path
          fetch                  Fetch and extract module's source
          update                 Update package indexes for content storages

        OPTIONS

          --install-to=[name]    Short name of CompUnit::Repository to install to


        FLAGS

          --verbose              More detailed output
          --force                Continue each phase regardless of failures
          --dry                  Run all phases except the actual installations
          --upgrade              Use the highest known matching version candidate

          --/tests               Skip the testing phase
          --/depends             Do not fetch runtime dependencies
          --/test-depends        Do not fetch test dependencies
          --/build-depends       Do not fetch build dependencies
        END_USAGE
}
